#Область ПрограммныйИнтерфейс

Процедура СериализоватьБазуВХМЛ(ЗаписьХМЛ, ВыгружатьКартинки) Экспорт
	
	ЗаписьХМЛ.ЗаписатьОбъявлениеXML();
	ЗаписьХМЛ.ЗаписатьНачалоЭлемента("data");
	ЗаписьХМЛ.ЗаписатьАтрибут("source", СтрШаблон("%1 ver: %2", Метаданные.Имя, Метаданные.Версия));
	
	ТипыМетаданных = Новый Массив(3);
	ТипыМетаданных[0] = Метаданные.Справочники;
	ТипыМетаданных[1] = Метаданные.Документы;
	ТипыМетаданных[2] = Метаданные.РегистрыСведений;
	
	Для каждого Метаданное Из ТипыМетаданных Цикл
		Для каждого ЭкзМетаданного Из Метаданное Цикл
			
			Если Не ВыгружатьКартинки
				И (ЭкзМетаданного = Метаданные.РегистрыСведений.ПринадлежностьФайлов
					Или ЭкзМетаданного = Метаданные.Справочники.ПрикрепленныеФайлы) Тогда
					
				Продолжить
			КонецЕсли;
			
			Если Метаданное = Метаданные.РегистрыСведений Тогда
				ДанныеДляВыгрузки = ЗапросПоНеобъектномуМетаданному(ЭкзМетаданного);
				ЗаписатьXML(ЗаписьХМЛ, ДанныеДляВыгрузки);
			Иначе
				ОбъектыДляВыгрузки = ЗапросПоОбъектномуМетаданному(ЭкзМетаданного);
				Для каждого СсылкаОбъекта Из ОбъектыДляВыгрузки Цикл
					ВыгружаемыйОбъект = СсылкаОбъекта.ПолучитьОбъект();
					ЗаписатьXML(ЗаписьХМЛ, ВыгружаемыйОбъект);
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	ЗаписьХМЛ.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ПрочитатьБазуИзХМЛ(ЧтениеХМЛ) Экспорт
	//Чт = Новый ЧтениеXML;
	ЧтениеХМЛ.Прочитать();
	ЧтениеХМЛ.Прочитать();
	Пока ВозможностьЧтенияXML(ЧтениеХМЛ) Цикл
		
		ДанныеДляЗагрузки = ПрочитатьXML(ЧтениеХМЛ);
		ДанныеДляЗагрузки.ОбменДанными.Загрузка = Истина;
		Попытка
	 		ДанныеДляЗагрузки.Записать();
		Исключение
			Сообщить(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла
	
КонецПроцедуры

Процедура УдалитьДублиПредопределенныхБезСсылок() Экспорт
	
	СписокОбрабатываемых = Новый Массив;
	Для каждого МетаданноеСпр Из Метаданные.Справочники Цикл
		СписокОбрабатываемых.Добавить(МетаданноеСпр.ПолноеИмя())
	КонецЦикла;
	
	Для каждого ИмяОбрабатываемого Из СписокОбрабатываемых Цикл
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Справ1.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных,
		|	Справ1.Ссылка КАК Ссылка
		|ИЗ
		|	&Справочник1 КАК Справ1,
		|	&Справочник1 КАК Справ2
		|ГДЕ
		|	Справ1.ИмяПредопределенныхДанных = Справ2.ИмяПредопределенныхДанных
		|	И Справ1.Ссылка <> Справ2.Ссылка
		|	И Справ1.Предопределенный = ИСТИНА
		|	И Справ2.Предопределенный = ИСТИНА
		|
		|СГРУППИРОВАТЬ ПО
		|	Справ1.ИмяПредопределенныхДанных,
		|	Справ1.Ссылка
		|ИТОГИ ПО
		|	ИмяПредопределенныхДанных");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Справочник1", ИмяОбрабатываемого);
		
		ВыборкаИтоги = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаИтоги.Следующий() Цикл
			
			ДубльБезСсылок = Неопределено;
			
			ВыборкаДетали = ВыборкаИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаДетали.Следующий() Цикл
				ТЗСсылок = НайтиПоСсылкам(ОбщегоНазначения.ЗначениеВМассив(ВыборкаДетали.Ссылка));
				Если ТЗСсылок.Количество() = 0 Тогда
					ДубльБезСсылок = ВыборкаДетали.Ссылка;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если ЗначениеЗаполнено(ДубльБезСсылок) Тогда
				ОбДубль = ДубльБезСсылок.ПолучитьОбъект();
				ОбДубль.ИмяПредопределенныхДанных = "";
				ОбДубль.ПометкаУдаления = Истина;
				ОбДубль.Записать();
				УдалитьОбъекты(ОбщегоНазначения.ЗначениеВМассив(ДубльБезСсылок), Ложь);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры


#КонецОбласти

#Область CлужебныеПроцедурыИФункции

Функция ЗапросПоОбъектномуМетаданному(Метаданное)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка
	|ИЗ
	|	&ПолныйПутьКОбъекту КАК Таблица";
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&ПолныйПутьКОбъекту", Метаданное.ПолноеИмя());
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Функция ЗапросПоНеобъектномуМетаданному(Метаданное)
	
	Менеджер = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Метаданное.ПолноеИмя());
	НаборЗаписей = Менеджер.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	
	Возврат НаборЗаписей;
	
	//Запрос = Новый Запрос;
	//ТекстЗапроса = 
	//"ВЫБРАТЬ
	//|	*
	//|ИЗ
	//|	&ПолныйПутьКОбъекту КАК Таблица";
	//Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&ПолныйПутьКОбъекту", Метаданное.ПолноеИмя());
	//Возврат Запрос.Выполнить().Выгрузить();
	//
КонецФункции

#КонецОбласти