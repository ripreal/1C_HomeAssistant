
#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	ИзмерительТО = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Автомобиль, "ИзмерительТО");
	
	Для Каждого ТекСтрока Из ВыполненныеРаботы Цикл
		Для каждого ВидТО Из Документы.РегистрацииТО.ПолучитьВидыТО() Цикл
			
			Набор = РегистрыСведений.ЗарегистрированныеТО.СоздатьНаборЗаписей();
			Набор.Отбор.Автомобиль.Установить(Автомобиль);
			Набор.Отбор.РегламентнаяРабота.Установить(ТекСтрока.РегламентнаяРабота);
			Набор.Отбор.НомерТО.Установить(ТекСтрока[ВидТО + "Номер"]);
			Набор.Прочитать();
			Набор.Очистить();
			
			Если ТекСтрока[ВидТО + "Статус"] = Перечисления.СтатусыВыполненияТО.Пройдено
				И ЗначениеЗаполнено(ТекСтрока[ВидТО + "ПоФакту"]) Тогда

				Движение = Набор.Добавить();
				
				Если ИзмерительТО = Справочники.Автомобили.ИзмерительТО_ВРЕМЯ() Тогда
					Движение.Период = ТекСтрока[ВидТО + "ПоПлану"];
				ИначеЕсли ИзмерительТО = Справочники.Автомобили.ИзмерительТО_ПРОБЕГ() Тогда
					Движение.Период = Дата;
				Иначе
					ВызватьИсключение "Not implemented!";
				КонецЕсли;
				
				Движение.ПоПлану = ТекСтрока[ВидТО + "ПоПлану"];
				Движение.ПоФакту = ТекСтрока[ВидТО + "ПоФакту"];
				Движение.Автомобиль = Автомобиль;
				Движение.РегламентнаяРабота = ТекСтрока.РегламентнаяРабота;
				Движение.СтатусТО = ТекСтрока[ВидТО + "Статус"];
				Движение.НомерТО = ТекСтрока[ВидТО + "Номер"];
				Движение.Сумма = ТекСтрока[ВидТО + "Сумма"];
				Движение.Внеплановое = ТекСтрока[ВидТО + "Внеплановое"];
				
			КонецЕсли;
			
			Набор.Записать(Истина);
			
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Для Каждого ТекСтрока Из ВыполненныеРаботы Цикл
		Для каждого ВидТО Из Документы.РегистрацииТО.ПолучитьВидыТО() Цикл
			Если ЗначениеЗаполнено(ТекСтрока[ВидТО + "ПоФакту"]) Тогда
				
				Набор = РегистрыСведений.ЗарегистрированныеТО.СоздатьНаборЗаписей();
				Набор.Отбор.Автомобиль.Установить(Автомобиль);
				Набор.Отбор.РегламентнаяРабота.Установить(ТекСтрока.РегламентнаяРабота);
				Набор.Отбор.НомерТО.Установить(ТекСтрока[ВидТО + "Номер"]);
				Набор.Прочитать();
				Набор.Очистить();
				
				Набор.Записать(Истина);
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти