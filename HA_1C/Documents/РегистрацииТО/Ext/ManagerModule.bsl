
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Представление = СтрШаблон(НСтр("ru = 'ТО №%1 от %2'; en = 'Car check №%1 dated %2'"), 
		Данные.ТекущийНомерТО, Формат(Данные.Дата, "ДФ=dd/MM/yyyy"));
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Поля = Новый Массив;
	Поля.Добавить("Дата");
	Поля.Добавить("ТекущийНомерТО");
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьВидыТО() Экспорт
	
	ВидыТО = Новый Массив;
	ВидыТО.Добавить(ИмяПрошлогоТО());
	ВидыТО.Добавить(ИмяТекущееТО());
	ВидыТО.Добавить(ИмяСледующееТО());
	
	Возврат ВидыТО;
	
КонецФункции

Функция ИмяПрошлогоТО() Экспорт
	
	Возврат "ПрошлоеТО";
	
КонецФункции

Функция ИмяТекущееТО() Экспорт
	
	Возврат "ТекущееТО";
	
КонецФункции

Функция ИмяСледующееТО() Экспорт
	
	Возврат "СледующееТО";
	
КонецФункции

Функция ТаблицаПланТО(ДатаАнализа, Автомобиль = Неопределено) Экспорт
	
	Проверки.СвойствоЗаполнено(ДатаАнализа);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВлЗапрос.Интервал КАК Интервал,
	|	ВлЗапрос.РегламентнаяРабота КАК РегламентнаяРабота,
	|	ВлЗапрос.ДатаПриобретения КАК ДатаПриобретения,
	|	ВлЗапрос.ТекущийНомерТО КАК НомерТОТекущий,
	|	ВлЗапрос.Автомобиль КАК Автомобиль,
	|	ВлЗапрос.Интервал / 12 КАК ИнтервалВнутригодовой
	|ПОМЕСТИТЬ ВлЗапрос
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВлЗапрос.Интервал КАК Интервал,
	|		ВлЗапрос.РегламентнаяРабота КАК РегламентнаяРабота,
	|		ВлЗапрос.ДатаПриобретения КАК ДатаПриобретения,
	|		ВЫБОР
	|			КОГДА (ВЫРАЗИТЬ(ВлЗапрос.ТекущийНомерТО КАК ЧИСЛО(13, 0))) = ВлЗапрос.ТекущийНомерТО
	|				ТОГДА ВлЗапрос.ТекущийНомерТО
	|			ИНАЧЕ ВЫРАЗИТЬ(ВлЗапрос.ТекущийНомерТО - 0.5 КАК ЧИСЛО(13, 0))
	|		КОНЕЦ КАК ТекущийНомерТО,
	|		ВлЗапрос.Автомобиль КАК Автомобиль
	|	ИЗ
	|		(ВЫБРАТЬ
	|			АвтомобилиРегламентныеРаботы.Интервал КАК Интервал,
	|			АвтомобилиРегламентныеРаботы.РегламентнаяРабота КАК РегламентнаяРабота,
	|			АвтомобилиРегламентныеРаботы.Ссылка.ДатаПриобретения КАК ДатаПриобретения,
	|			ВЫРАЗИТЬ(РАЗНОСТЬДАТ(АвтомобилиРегламентныеРаботы.Ссылка.ДатаПриобретения, &ДатаАнализа, МЕСЯЦ) / АвтомобилиРегламентныеРаботы.Интервал КАК ЧИСЛО(10, 3)) КАК ТекущийНомерТО,
	|			АвтомобилиРегламентныеРаботы.Ссылка КАК Автомобиль
	|		ИЗ
	|			Справочник.Автомобили.РегламентныеРаботы КАК АвтомобилиРегламентныеРаботы
	|		ГДЕ
	|			(&Автомобиль = НЕОПРЕДЕЛЕНО
	|					ИЛИ АвтомобилиРегламентныеРаботы.Ссылка = &Автомобиль)) КАК ВлЗапрос) КАК ВлЗапрос
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВлЗапрос.Интервал КАК Интервал,
	|	ВлЗапрос.РегламентнаяРабота КАК РегламентнаяРабота,
	|	ВлЗапрос.ДатаПриобретения КАК ДатаПриобретения,
	|	ВлЗапрос.Автомобиль КАК Автомобиль,
	|	ВлЗапрос.НомерТОТекущий КАК НомерТОТекущий,
	|	ВлЗапрос.ИнтервалВнутригодовой КАК ИнтервалВнутригодовой,
	|	0 КАК Поле1
	|ПОМЕСТИТЬ ТОВнутригодовой
	|ИЗ
	|	ВлЗапрос КАК ВлЗапрос
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВлЗапрос.Интервал,
	|	ВлЗапрос.РегламентнаяРабота,
	|	ВлЗапрос.ДатаПриобретения,
	|	ВлЗапрос.Автомобиль,
	|	ВлЗапрос.НомерТОТекущий - 1,
	|	ВлЗапрос.ИнтервалВнутригодовой,
	|	1
	|ИЗ
	|	ВлЗапрос КАК ВлЗапрос
	|ГДЕ
	|	ВлЗапрос.ИнтервалВнутригодовой <> (ВЫРАЗИТЬ(ВлЗапрос.ИнтервалВнутригодовой КАК ЧИСЛО(13, 0)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВлЗапрос.Автомобиль КАК Автомобиль,
	|	ВлЗапрос.ДатаПриобретения КАК ДатаПриобретения,
	|	ВлЗапрос.РегламентнаяРабота КАК РегламентнаяРабота,
	|	ВлЗапрос.Интервал КАК Интервал,
	|	ВлЗапрос.НомерТОТекущий - ВлЗапрос.ИнтервалВнутригодовой КАК НомерТОПрошлый,
	|	ВЫБОР
	|		КОГДА РАЗНОСТЬДАТ(ВлЗапрос.ДатаПриобретения, &ДатаАнализа, МЕСЯЦ) < ВлЗапрос.Интервал
	|			ТОГДА NULL
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ВлЗапрос.ДатаПриобретения, МЕСЯЦ, (ВлЗапрос.НомерТОТекущий - 1) * ВлЗапрос.Интервал)
	|	КОНЕЦ КАК ПрошлоеТОПоПлану,
	|	ВлЗапрос.НомерТОТекущий КАК НомерТОТекущий,
	|	ВЫБОР
	|		КОГДА ВлЗапрос.НомерТОТекущий = 0
	|			ТОГДА ДОБАВИТЬКДАТЕ(ВлЗапрос.ДатаПриобретения, МЕСЯЦ, ВлЗапрос.Интервал)
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ВлЗапрос.ДатаПриобретения, МЕСЯЦ, ВлЗапрос.НомерТОТекущий * ВлЗапрос.Интервал)
	|	КОНЕЦ КАК ТекущееТОПоПлану,
	|	ВлЗапрос.НомерТОТекущий + ВлЗапрос.ИнтервалВнутригодовой КАК НомерТОСледующий,
	|	ДОБАВИТЬКДАТЕ(ВлЗапрос.ДатаПриобретения, МЕСЯЦ, (ВлЗапрос.НомерТОТекущий + 1) * ВлЗапрос.Интервал) КАК СледующееТОПоПлану
	|ПОМЕСТИТЬ ТО_ПЛАН
	|ИЗ
	|	ТОВнутригодовой КАК ВлЗапрос
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТО_ПЛАН.Автомобиль КАК Автомобиль,
	|	ТО_ПЛАН.РегламентнаяРабота КАК РегламентнаяРабота,
	|	ТО_ПЛАН.НомерТОПрошлый КАК ПрошлоеТОНомер,
	|	ТО_ПЛАН.ПрошлоеТОПоПлану КАК ПрошлоеТОПоПлану,
	|	ЗарегистрированныеТОПрошлые.ПоФакту КАК ПрошлоеТОПоФакту,
	|	ЕСТЬNULL(ЗарегистрированныеТОПрошлые.СтатусТО, ЗНАЧЕНИЕ(Перечисление.СтатусыВыполненияТО.НеПройдено)) КАК ПрошлоеТОСтатус,
	|	ТО_ПЛАН.НомерТОТекущий КАК ТекущееТОНомер,
	|	ТО_ПЛАН.ТекущееТОПоПлану КАК ТекущееТОПоПлану,
	|	ЗарегистрированныеТОТекущие.ПоФакту КАК ТекущееТОПоФакту,
	|	ЕСТЬNULL(ЗарегистрированныеТОТекущие.СтатусТО, ЗНАЧЕНИЕ(Перечисление.СтатусыВыполненияТО.НеПройдено)) КАК ТекущееТОСтатус,
	|	ТО_ПЛАН.НомерТОСледующий КАК СледующееТОНомер,
	|	ТО_ПЛАН.СледующееТОПоПлану КАК СледующееТОПоПлану,
	|	ЗарегистрированныеТОСледующие.ПоФакту КАК СледующееТОПоФакту,
	|	ЕСТЬNULL(ЗарегистрированныеТОСледующие.СтатусТО, ЗНАЧЕНИЕ(Перечисление.СтатусыВыполненияТО.НеПройдено)) КАК СледующееТОСтатус,
	|	ЗарегистрированныеТОПрошлые.Сумма КАК ПрошлоеТОСумма,
	|	ЗарегистрированныеТОТекущие.Сумма КАК ТекущееТОСумма,
	|	ЗарегистрированныеТОСледующие.Сумма КАК СледующееТОСумма
	|ИЗ
	|	ТО_ПЛАН КАК ТО_ПЛАН
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗарегистрированныеТО КАК ЗарегистрированныеТОТекущие
	|		ПО ТО_ПЛАН.РегламентнаяРабота = ЗарегистрированныеТОТекущие.РегламентнаяРабота
	|			И ТО_ПЛАН.НомерТОТекущий = ЗарегистрированныеТОТекущие.НомерТО
	|			И ТО_ПЛАН.Автомобиль = ЗарегистрированныеТОТекущие.Автомобиль
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗарегистрированныеТО КАК ЗарегистрированныеТОПрошлые
	|		ПО ТО_ПЛАН.Автомобиль = ЗарегистрированныеТОПрошлые.Автомобиль
	|			И ТО_ПЛАН.РегламентнаяРабота = ЗарегистрированныеТОПрошлые.РегламентнаяРабота
	|			И ТО_ПЛАН.НомерТОПрошлый = ЗарегистрированныеТОПрошлые.НомерТО
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗарегистрированныеТО КАК ЗарегистрированныеТОСледующие
	|		ПО ТО_ПЛАН.Автомобиль = ЗарегистрированныеТОСледующие.Автомобиль
	|			И ТО_ПЛАН.РегламентнаяРабота = ЗарегистрированныеТОСледующие.РегламентнаяРабота
	|			И ТО_ПЛАН.НомерТОСледующий = ЗарегистрированныеТОСледующие.НомерТО
	|
	|УПОРЯДОЧИТЬ ПО
	|	Автомобиль,
	|	РегламентнаяРабота,
	|	ТекущееТОНомер");
	Запрос.УстановитьПараметр("Автомобиль", Автомобиль);
	Запрос.УстановитьПараметр("ДатаАнализа", ДатаАнализа);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ДатаТОПоИнтервалу(ДатаПриобретения = Неопределено, ВидТО, ДатаДокумента) Экспорт
	
	Если ДатаПриобретения = Неопределено Тогда
		ДатаПриобретения = ДатаДокумента;
	КонецЕсли;
	
	ДатаАнализа = Дата(Год(ДатаДокумента), Месяц(ДатаПриобретения), День(ДатаПриобретения));
	
	Если ВидТО = ИмяТекущееТО() Тогда
		Если ДатаДокумента > ДатаАнализа Тогда
			Возврат ДатаАнализа;
		Иначе
			Возврат НачалоГода(ДатаАнализа) - 1
		КонецЕсли;
	ИначеЕсли ВидТО = ИмяСледующееТО() Тогда
		СледующийГодДатаАнализа = ДобавитьМесяц(ДатаАнализа, 12);
		СледующийГодДатаДокумента = ДобавитьМесяц(ДатаДокумента, 12);
		Если СледующийГодДатаДокумента > СледующийГодДатаАнализа Тогда
			Возврат СледующийГодДатаАнализа;
		Иначе
			Возврат НачалоГода(СледующийГодДатаАнализа) - 1;
		КонецЕсли;
	ИначеЕсли ВидТО = ИмяПрошлогоТО() Тогда
		КонецПрошлыйГодДатаАнализа = ДобавитьМесяц(ДатаАнализа, -12);
		КонецПрошлыйГодДатаДокумента = ДобавитьМесяц(ДатаДокумента, -12);
		Если КонецПрошлыйГодДатаДокумента > КонецПрошлыйГодДатаАнализа Тогда
			Возврат КонецПрошлыйГодДатаАнализа;
		Иначе
			Возврат НачалоГода(КонецПрошлыйГодДатаАнализа) - 1;
		КонецЕсли;
	КонецЕсли;
	
	ВызватьИсключение "Not implemented";
	
КонецФункции

#КонецОбласти
