#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Модуль "актОтчетыОбщиеФункции" 
////////////////////////////////////////////////////////////////////////////////

// Выводит отчет в Таблицу значений
Функция ВывестиОтчетСКД_ТЗ(СхемаКомпоновкиДанных, НастройкиОтчета = Неопределено, ВнешниеНаборыДанных = Неопределено, Результат = Неопределено) Экспорт
	
	Если НастройкиОтчета = Неопределено Тогда
		НастройкиОтчета = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	КонецЕсли;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;

	НазначитьПараметрВывода(НастройкиОтчета, "ВертикальноеРасположениеОбщихИтогов", РасположениеИтоговКомпоновкиДанных.Нет);
	НазначитьПараметрВывода(НастройкиОтчета, "ГоризонтальноеРасположениеОбщихИтогов", РасположениеИтоговКомпоновкиДанных.Нет);
		
	МакетКомпоновки = КомпоновщикМакета.Выполнить(
		СхемаКомпоновкиДанных, НастройкиОтчета,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"), Ложь);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных,, Истина);
	
	Если Результат = Неопределено Тогда
		РезультатОтчета = Новый ТаблицаЗначений;
	Иначе
		РезультатОтчета = Результат;
	КонецЕсли;
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(РезультатОтчета);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Возврат РезультатОтчета;
	
КонецФункции

// Выводит отчет в ТабличныйДокумент
//
// Параметры:
//  СхемаКомпоновкиДанных	 - 	 - 
//  НастройкиОтчета			 - 	 - 
// 
// Возвращаемое значение:
//   - 
//
Функция ВывестиОтчетСКД_Таблица(СхемаКомпоновкиДанных, НастройкиОтчета = Неопределено, ВнешниеНаборыДанных = Неопределено) Экспорт
	
	Если НастройкиОтчета = Неопределено Тогда
		НастройкиОтчета = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	КонецЕсли;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
		
	МакетКомпоновки = КомпоновщикМакета.Выполнить(
		СхемаКомпоновкиДанных, НастройкиОтчета,,,, Ложь);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных,, Истина);
	
	Результат = Новый ТабличныйДокумент;
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

// Устанавливает предопределенный параметр вывода в отчете СКД
//
// Параметры:
//  Настройки          -НастройкиКомпоновкиДанных- Настройки
//  ИмяПараметра		 - Строка - Имя
//  ЗначениеПараметра	 - Произвольный - зависит от имени параметра
//  
// Пример:
// 
// НазначитьПараметрВывода(НастройкиОтчета, "ВертикальноеРасположениеОбщихИтогов", РасположениеИтоговКомпоновкиДанных.Нет);
//
Процедура НазначитьПараметрВывода(Настройки, ИмяПараметра, ЗначениеПараметра)
	
	ПараметрВывода = Настройки.ПараметрыВывода.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	Если ПараметрВывода <> Неопределено Тогда
		ПараметрВывода.Использование = Истина;
		ПараметрВывода.Значение = ЗначениеПараметра;
	КонецЕсли;
	
КонецПроцедуры
