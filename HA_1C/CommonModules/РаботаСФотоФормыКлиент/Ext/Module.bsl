////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для работы с фото со стороны клиента
//
// На форме должны быть размещены следующие реквизиты: ФотоЗагружены (булево), ИндексПоследнегоФото (Число), СписокФото (Список значений)
//
// На форме должны быть проверены обработчики ПриЗаписиНаСервере() и размещена область  РаботаСФото
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Процедура ПриСменеСтраницыСФото(Форма, ТекущаяСтраница) Экспорт
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если ТекущаяСтраница <> Элементы.СтраницаФото Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) И Не Форма.ФотоЗагружены Тогда
		Форма.ПодключитьОбработчикОжидания("Подключаемый_НачатьЗагрузкуФото", 0.1, Истина);
	Иначе
		Форма.ФотоЗагружены = Истина;
		Элементы.ДекорацияПустоеФото.Видимость = Истина;
		Элементы.ДекорацияЗагрузкаКартинки.Видимость = Ложь;
		Элементы.ДекорацияЗагрузкаТекст.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура Подключаемый_СсылкаНаПредставлениеДанныхНажатие(Форма, Элемент, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ЗаменяемоеФото = Число(Прав(Элемент.Имя, 1)) - 1;
	НачатьСозданиеНовогоФото(Форма, Элемент, ЗаменяемоеФото)
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НачатьСозданиеНовогоФото(Форма, Элемент = Неопределено, ИндексЗаменяемогоФото = Неопределено) Экспорт
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	ДополнительныеПараметры.Вставить("ИндексЗаменяемогоФото", ИндексЗаменяемогоФото);
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ЗавершитьСозданиеНовогоФото", РаботаСФотоФормыКлиент.ЭтотОбъект, ДополнительныеПараметры);
	СписокВыбора = Новый СписокЗначений;
	
	СписокВыбора.Добавить("ОткрытьФайл", НСтр("ru = 'Выбрать из устройства'; en = 'Choose from device'"),, БиблиотекаКартинок.ОткрытьФайл);
	СписокВыбора.Добавить("СделатьФото", НСтр("ru = 'Сделать фото'; en = 'Make the photo'"),, БиблиотекаКартинок.Камера48);
	
	Если Элемент <> Неопределено И ЗначениеЗаполнено(Форма[Элемент.Имя]) Тогда
		СписокВыбора.Вставить(0, "ПросмотретьФайл", НСтр("ru = 'Предпросмотр'; en = 'Preview'"),, БиблиотекаКартинок.Найти);
		СписокВыбора.Добавить("ИзменитьФото", 
			НСтр("ru = 'Изменить фото'; en = 'Edit photo'"),, 
			БиблиотекаКартинок.Изменить);
	КонецЕсли;
	
	Форма.ПоказатьВыборИзМеню(ОписаниеОповещенияОЗакрытии, СписокВыбора, Элемент);
	
КонецПроцедуры

Процедура ЗавершитьСозданиеНовогоФото(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДополнительныеПараметры.Форма;
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если ДополнительныеПараметры.Элемент = Неопределено Тогда
		// Создается новое фото, добавим для него элемент
		ИмяЭлемента = Форма.РеквизитФормыДляФотоКлиент();
		Элементы.СтраницыСписокФото.ТекущаяСтраница = Элементы["СтраницаЭлементФото" + Строка(Форма.ИндексПоследнегоФото)];
		ДополнительныеПараметры.Элемент = Элементы[ИмяЭлемента];
	КонецЕсли;
	
	Если ВыбранныйЭлемент.Значение = "ОткрытьФайл" Тогда
		ОбработатьОткрытиеФайла(Форма, ДополнительныеПараметры.Элемент, ДополнительныеПараметры.ИндексЗаменяемогоФото);
	ИначеЕсли ВыбранныйЭлемент.Значение = "ПросмотретьФайл" Тогда
		ОбработатьПросмотрФайла(Форма, ДополнительныеПараметры.Элемент)
	ИначеЕсли ВыбранныйЭлемент.Значение = "ИзменитьФото" Тогда
		ОбработатьИзменениеФото(Форма, ДополнительныеПараметры.Элемент, ДополнительныеПараметры.ИндексЗаменяемогоФото)
	Иначе
		ОбработатьНажатиеНаФото(Форма, ДополнительныеПараметры.Элемент, ДополнительныеПараметры.ИндексЗаменяемогоФото);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОткрытиеФайла(Форма, Элемент, ИндексЗаменяемогоФото) Экспорт
	
	ИмяФото = Элемент.Имя;
	
	Данные = ПрикрепленныеФайлыКлиент.НоваяКартинкаИзФайла(Форма.УникальныйИдентификатор);
	ДобавитьФотоВСписок(Форма, ИмяФото, Данные, ИндексЗаменяемогоФото);
	
КонецПроцедуры

Процедура ОбработатьНажатиеНаФото(Форма, Элемент, ИндексЗаменяемогоФото) Экспорт
	
	ИмяФото = Элемент.Имя;
	
	Данные = ПрикрепленныеФайлыКлиент.НоваяФотография(, Форма.УникальныйИдентификатор);
	ДобавитьФотоВСписок(Форма, ИмяФото, Данные, ИндексЗаменяемогоФото);
	
КонецПроцедуры

Процедура ОбработатьПросмотрФайла(Форма, Элемент) 
	
	СсылкаНаФото = Форма[Элемент.Имя];
	УправлениеМультимедиаКлиент.ОткрытьКартинкуВГалерее(СсылкаНаФото);
	
КонецПроцедуры

Процедура ОбработатьИзменениеФото(Форма, Элемент, ИндексЗаменяемогоФото)
	
	СсылкаНаФото = Форма[Элемент.Имя];
	Данные = ПрикрепленныеФайлыКлиент.НоваяОтредактированнаяКартинкаИзФайла(СсылкаНаФото, Форма.УникальныйИдентификатор);
	ДобавитьФотоВСписок(Форма, Элемент.Имя, Данные, ИндексЗаменяемогоФото);
	
КонецПроцедуры

Процедура ДобавитьФотоВСписок(Форма, ИмяФото, Данные, ИндексЗаменяемогоФото = Неопределено)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если ЗначениеЗаполнено(Данные) Тогда
		Если ИндексЗаменяемогоФото <> Неопределено 
			И Форма.СписокФото.Количество() > ИндексЗаменяемогоФото Тогда 
			Форма.СписокФото.Удалить(ИндексЗаменяемогоФото);
			Форма.СписокФото.Вставить(ИндексЗаменяемогоФото, Данные);
		Иначе
			Форма.СписокФото.Добавить(Данные);
		КонецЕсли;
		Форма[ИмяФото] = Данные.НавигационнаяСсылка;
	КонецЕсли
	
КонецПроцедуры

#КонецОбласти