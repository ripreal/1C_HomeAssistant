
////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для работы документами сервисных работ на клиенте и сервере
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Процедура ОбновитьЭлементыСуммы(Форма, ВидыТО) Экспорт
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Для каждого ВидТО ИЗ ВидыТО Цикл
		ЭлементРеквизита = ФормыОбщиеДействияСервер.ДобавитьРеквизитСоСвязаннымЭлементомНаФорму(
			Форма, 
			ВидТО + ФормыСервисныеРаботыКлиентСервер.ИмяРеквизитаСуммы(), 
			0,
			Элементы.ГруппаИтоги,
			ОбщегоНазначения.ОписаниеТипаЧисло(7,, ДопустимыйЗнак.Неотрицательный)
		);
		ЭлементРеквизита.Заголовок = НСтр("ru = 'Итого'; en = 'Total'");
		ЭлементРеквизита.Ширина = 7;
		ЭлементРеквизита.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		ЭлементРеквизита.Вид = ВидПоляФормы.ПолеНадписи;
		ЭлементРеквизита.Шрифт = Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста,,, Истина);
		ЭлементРеквизита.РастягиватьПоГоризонтали = Истина;
		ЭлементРеквизита.Формат = "ЧЦ=7; ЧРГ=' '; ЧН='0 rub'; ЧГ=3,0; ЧФ='Ч rub'";
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПробегЕслиНовыйДокумент(Форма) Экспорт
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДокументСервиса.Пробег КАК Пробег,
	|	ДокументСервиса.Дата КАК Дата
	|ИЗ
	|	&ДокументСервиса КАК ДокументСервиса
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ");
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДокументСервиса", Объект.Ссылка.Метаданные().ПолноеИмя());
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пробег = 0;
	Если Выборка.Следующий() Тогда
		Пробег = Выборка.Пробег;
	КонецЕсли;
	
	Объект.Пробег = Пробег;
	
КонецПроцедуры

Функция ТекущийНомерТО(знач Автомобиль, знач ДатаАнализа, знач Пробег) Экспорт
	
	Если Не ЗначениеЗаполнено(Автомобиль) Или Не ЗначениеЗаполнено(ДатаАнализа) Тогда
		Возврат 0;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(ВлЗапрос.ТекущийНомерТО КАК ЧИСЛО(13, 0))) = ВлЗапрос.ТекущийНомерТО
	|			ТОГДА ВлЗапрос.ТекущийНомерТО
	|		ИНАЧЕ ВЫРАЗИТЬ(ВлЗапрос.ТекущийНомерТО - 0.5 КАК ЧИСЛО(13, 0))
	|	КОНЕЦ КАК ТекущийНомерТО
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР КОГДА &ИзмерительТО = ""ВРЕМЯ"" ТОГДА ВЫРАЗИТЬ(РАЗНОСТЬДАТ(&ДатаПриобретения, &ДатаАнализа, МЕСЯЦ) / 12 КАК ЧИСЛО(10, 3)) КОГДА  &ИзмерительТО = ""ПРОБЕГ"" ТОГДА ВЫРАЗИТЬ(&Пробег / &МежсервисныйИнтервал КАК ЧИСЛО(10, 3)) КОНЕЦ КАК ТекущийНомерТО) КАК ВлЗапрос");
	Запрос.УстановитьПараметр("ДатаПриобретения", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Автомобиль, "ДатаПриобретения"));
	Запрос.УстановитьПараметр("ДатаАнализа", ДатаАнализа);
	Запрос.УстановитьПараметр("ИзмерительТО", Автомобиль.ИзмерительТО);
	Запрос.УстановитьПараметр("Пробег", Пробег);
	Запрос.УстановитьПараметр("МежсервисныйИнтервал", Автомобиль.МежсервисныйИнтервал);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.ТекущийНомерТО;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#КонецОбласти