
////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для работы с фото со стороны сервера
//
////////////////////////////////////////////////////////////////////////////////


#Область ПрограммныйИнтерфейс

Процедура ЗагрузитьФотоПроекта(Форма) Экспорт
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Форма.СписокФото = ПрикрепленныеФайлыСервер.ПрочитатьСписокФайлов(Объект.Ссылка);
	
	Для каждого СтрокаФайла Из Форма.СписокФото Цикл
		ИмяЭлемента = РеквизитФормыДляФото(Форма);
		Форма[ИмяЭлемента] = СтрокаФайла.Значение.НавигационнаяСсылка;
	КонецЦикла;
	
	Форма.ФотоЗагружены = Истина;
	
КонецПроцедуры 

Функция РеквизитФормыДляФото(Форма) Экспорт
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Форма.ИндексПоследнегоФото = Форма.ИндексПоследнегоФото + 1;
	
	ИмяФото = РаботаСФотоФормыКлиентСервер.ИмяРеквизитаФото() + Форма.ИндексПоследнегоФото;
	
	ЭтоНовый = Ложь;
	РеквизитФото = ФормыОбщиеДействияСервер.НайтиДобавитьРеквизитФормы(
		ЭтоНовый, 
		Форма, 
		ИмяФото, 
		"Строка"
	);
	
	Если Не ЭтоНовый Тогда
		Возврат РеквизитФото.Имя;
	КонецЕсли;
	
	СтраницаЭлементФото = Элементы.Вставить(
		"СтраницаЭлементФото" + Форма.ИндексПоследнегоФото, 
		Тип("ГруппаФормы"), 
		Элементы.СтраницыСписокФото,
		Элементы.СтраницаЭлементФото0
	);
	
	ЭлементФото = Элементы.Добавить(
		ИмяФото, 
		Тип("ПолеФормы"),
		СтраницаЭлементФото
	);
		
	ЭлементФото.Вид         = ВидПоляФормы.ПолеКартинки;
	ЭлементФото.ПутьКДанным = РеквизитФото.Имя;
	ЭлементФото.Гиперссылка = Истина;
	ЭлементФото.РазмерКартинки = РазмерКартинки.Пропорционально; 
	ЭлементФото.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементФото.УстановитьДействие("Нажатие", "Подключаемый_СсылкаНаПредставлениеДанныхНажатие");
	
	Возврат ЭлементФото.Имя;
	
КонецФункции

Процедура ПриЗаписиНаСервере(Форма, Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт
	
	Если Форма.ФотоЗагружены Тогда
		ПрикрепленныеФайлыСервер.ОбработатьИЗаписатьДанныеМультимедиа(Форма.СписокФото, ТекущийОбъект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#КонецОбласти