//////////////////////////////////////////////////////////////////////////////////////////
// Интерфейс работы с прикрепленными файлами на мобильном устройстве (сервер)
// 
//////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Создает или изменяет элемент справочника ПрикрепленныеФайлы и возвращает справочник-объект с сохраненными изменениями.
//	Двоичные даные в реквизите "ДанныеФайла" перезапиываются только если в качестве "НавигационнойСсылки" используется адрес вреиенного хранилища
//
// Параметры:
//  ПакетМультимедиа		 - Структура							 - упакованные данные мультимедиа (см. ПрикрепленныеФайлыКлиент.УпаковатьДанныеМультимедиа)
// 
// Возвращаемое значение:
//   Булево - ИСТИНА, если элемент справочника записан 
//
Функция ЗаписатьДанныеМультимедиа(ПакетМультимедиа, ИндексВКоллекции = 0) Экспорт
	
	Если ТипЗнч(ПакетМультимедиа) <> Тип("Структура") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЭтоАдресВременногоХранилища(ПакетМультимедиа.НавигационнаяСсылка) Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(ПакетМультимедиа.НавигационнаяСсылка);
	КонецЕсли;
	
	Если (ДвоичныеДанные = Неопределено Или ДвоичныеДанные.Размер() = 0) И Не ЗначениеЗаполнено(ПакетМультимедиа.ПрикрепленныйФайл) Тогда
		// Нечего записывать
		Возврат Ложь;
	КонецЕсли;
	
	// Объект справочника получаем из ссылки или создаем новый
	ПрикрепленныйФайл = Неопределено;
	Если ЗначениеЗаполнено(ПакетМультимедиа.ПрикрепленныйФайл) Тогда
		ПрикрепленныйФайл = ПакетМультимедиа.ПрикрепленныйФайл.ПолучитьОбъект();
	Иначе
		ПрикрепленныйФайл = Справочники.ПрикрепленныеФайлы.СоздатьЭлемент();
	КонецЕсли;
	
	// Always update the date so that we're able to track rendering order in the gallery
	ПрикрепленныйФайл.ДатаСохранения = ТекущаяДата() + ИндексВКоллекции;
	
	ДатаСозданияФайла = ?(ЗначениеЗаполнено(ПакетМультимедиа.ДатаСоздания), ПакетМультимедиа.ДатаСоздания, ТекущаяДата());
	Если ПрикрепленныйФайл.ДатаСоздания <> ДатаСозданияФайла Тогда
		// Двоичные данные былии изменены
		ПрикрепленныйФайл.ДатаСоздания = ДатаСозданияФайла;
		ПрикрепленныйФайл.Наименование = ПрикрепленныеФайлыКлиентСервер.АвтоНаименованиеФайла(
			ПакетМультимедиа.СпособОткрытия, ДатаСозданияФайла);
	Иначе
		ПрикрепленныйФайл.Наименование = ПакетМультимедиа.Наименование;
	КонецЕсли;
	
	Если ДвоичныеДанные <> Неопределено Тогда
		ПрикрепленныйФайл.ДанныеФайла = Новый ХранилищеЗначения(ДвоичныеДанные, ПолучитьСжатиеДанных());
		ПрикрепленныйФайл.РазмерФайла = ДвоичныеДанные.Размер() / 1024;
	КонецЕсли;

	ПрикрепленныйФайл.СпособОткрытияФайла = ПакетМультимедиа.СпособОткрытия;
	ПрикрепленныйФайл.РасширениеФайла     = ПакетМультимедиа.Расширение;
	ПрикрепленныйФайл.Комментарий         = ПакетМультимедиа.Комментарий;
	
	ПрикрепленныйФайл.Записать();
	
	// Обновляем ссылку на прикрепленный файл в структуре его описания
	ПакетМультимедиа.ПрикрепленныйФайл = ПрикрепленныйФайл.Ссылка;
	
	Возврат Истина;
	
КонецФункции

// Записывает двоичные данные в базу с учетом владельца. 
//
// Параметры:
//  СписокОписанийФайлов		 - СписокЗначений - упакованные данные мультимедиа (см. ПрикрепленныеФайлыКлиент.УпаковатьДанныеМультимедиа)
//  ВладелецСсылка			 - ОбъектСсылка - Владелец прикрепленного файла или Неопределено (если нет владельца)
//
Процедура ОбработатьИЗаписатьДанныеМультимедиа(СписокОписанийФайлов, ВладелецСсылка) Экспорт
	
	Если (СписокОписанийФайлов = Неопределено) Тогда
		Возврат;
	КонецЕсли;
	
	МассивЗаписанныхФайлов = Новый Массив;
	
	НачатьТранзакцию();
	Попытка
		
		Для Каждого Данные Из СписокОписанийФайлов Цикл
			
			Если ЗаписатьДанныеМультимедиа(Данные.Значение, СписокОписанийФайлов.Индекс(Данные)) И ВладелецСсылка <> Неопределено Тогда
				МассивЗаписанныхФайлов.Добавить(Данные.Значение.ПрикрепленныйФайл);
			КонецЕсли;
			
		КонецЦикла;
		
		Если ВладелецСсылка <> Неопределено Тогда
			ОбновитьНаборФайловПоВладельцу(ВладелецСсылка, МассивЗаписанныхФайлов);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение НСтр("ru='Запись данных прикрепленного файла не состоялась! По причине: '" + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
КонецПроцедуры

// Удаляет прикрепленный файл и его связи с владельцами
//
// Параметры:
//  ПакетМультимедиа - СправочникСсылка.ПрикрепленныеФайлы - Ссылка на прикрепленный файл
//  Владелец		 - ОбъектСсылка или Неопределено - Ссылка на владельца. Если не задан, то прикрепелнный файл удаляется полностью из всех владельцев
//
Процедура УдалитьПрикрепленныйФайл(ПрикрепленныйФайл, Владелец = Неопределено) Экспорт
	Если (ПрикрепленныйФайл = Неопределено) Тогда
		Возврат;
	КонецЕсли;
		
	НачатьТранзакцию();
	Попытка
		
		//Чистим регистр сведений
 		НаборЗаписейРегистра = РегистрыСведений.ПринадлежностьФайлов.СоздатьНаборЗаписей(); 
		НаборЗаписейРегистра.Отбор.ПрикрепленныйФайл.Установить(ПрикрепленныйФайл); 
		
		Если Владелец <> Неопределено Тогда
			НаборЗаписейРегистра.Отбор.ВладелецФайла.Установить(Владелец); 	
		КонецЕсли;
		
 		НаборЗаписейРегистра.Прочитать(); 
 		НаборЗаписейРегистра.Очистить(); 
 		НаборЗаписейРегистра.Записать(Истина); 
		
		//Удалем сам прикрепленный файл из БД
		КакОбъект = ПрикрепленныйФайл.ПолучитьОбъект();
		КакОбъект.Удалить();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение НСтр("ru='Ошибка удаления прикрепленного файла `"+ПрикрепленныйФайл.Наименование+"`! По причине: '" + ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

// Возвращает алгоритм сжатия данных для записи прикрепленного файла
// 
// Возвращаемое значение:
//  СжатиеДанных - объект, описывающий сзатие данных
//
Функция ПолучитьСжатиеДанных() Экспорт
	
	Возврат Новый СжатиеДанных(7);
	
КонецФункции

// Обновляет записи регистра сведений "ПринадлежностьФайлов" по указаному владельцу
//
//Параметры:
//	ВладелецФайлов - ссылка на владельца файлов
//	СписокФайлов - список значений или массив ссылок справочника ПрикрепленныеФайлы
//
Процедура ОбновитьНаборФайловПоВладельцу(ВладелецФайлов, СписокФайлов) Экспорт
	
	МассивНовыхФайлов = ?(ТипЗнч(СписокФайлов) = Тип("Массив"), СписокФайлов, СписокФайлов.ВыгрузитьЗначения());
	
	// Создаем набор записей
	Набор = РегистрыСведений.ПринадлежностьФайлов.СоздатьНаборЗаписей();
	Набор.Отбор.ВладелецФайла.Установить(ВладелецФайлов);
	
	// Получаем текущий набор файлов указанного владельца для проверки их исключения из списка
	Набор.Прочитать();
	МассивИсключенныхФайлов = Набор.ВыгрузитьКолонку("ПрикрепленныйФайл");
	
	// Записываем новые файлы
	Набор.Очистить();
	Для каждого НовыйФайл Из МассивНовыхФайлов Цикл
		
		ЗаписьНабора = Набор.Добавить();
		ЗаписьНабора.ВладелецФайла = ВладелецФайлов;
		ЗаписьНабора.ПрикрепленныйФайл = НовыйФайл;
		
		// Удаляем файл из списка исключенных
		ПрежнийФайл = МассивИсключенныхФайлов.Найти(НовыйФайл);
		Если ПрежнийФайл <> Неопределено Тогда
			// В массиве должны остаться только те файлы, которые больше не связаны с данным владельцем
			МассивИсключенныхФайлов.Удалить(ПрежнийФайл);
		КонецЕсли;
		
	КонецЦикла;
	Набор.Записать(Истина);
	
	Если МассивИсключенныхФайлов.Количество() > 0 Тогда
		ПроверитьПринадлежностьФайловОбъектам(МассивИсключенныхФайлов);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает список данных прикрепленных файлов, подчиненных указанному владельцу
//
// Параметры:
//  ВладелецФайлов	 - Ссылка - Ссылка на объект-родитель
// 
// Возвращаемое значение:
//  СписокЗначений - список данных прикрепленных файлов
//
Функция ПрочитатьСписокФайлов(ВладелецФайла) Экспорт

	Результат = Новый СписокЗначений;
	Если ВладелецФайла = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВладелецФайла", ВладелецФайла);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПринадлежностьФайлов.ПрикрепленныйФайл.Ссылка КАК ПрикрепленныйФайл,
	|	ПринадлежностьФайлов.ПрикрепленныйФайл.Наименование КАК Наименование,
	|	ПринадлежностьФайлов.ПрикрепленныйФайл.Комментарий КАК Комментарий,
	|	ПринадлежностьФайлов.ПрикрепленныйФайл.СпособОткрытияФайла КАК СпособОткрытия,
	|	ПринадлежностьФайлов.ПрикрепленныйФайл.РасширениеФайла КАК Расширение,
	|	ПринадлежностьФайлов.ПрикрепленныйФайл.ДатаСоздания КАК ДатаСоздания,
	|	ПринадлежностьФайлов.ПрикрепленныйФайл.РазмерФайла КАК Размер,
	|	ПринадлежностьФайлов.ПрикрепленныйФайл.НеСинхронизировать КАК НеСинхронизировать,
	|	ПринадлежностьФайлов.ПрикрепленныйФайл.ДатаСохранения КАК ДатаСохранения
	|ИЗ
	|	РегистрСведений.ПринадлежностьФайлов КАК ПринадлежностьФайлов
	|ГДЕ
	|	ПринадлежностьФайлов.ВладелецФайла = &ВладелецФайла
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаСохранения";
		
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Пакет = ПрикрепленныеФайлыКлиентСервер.НоваяСтруктураПрикрепляемогоФайла();
		
		ЗаполнитьЗначенияСвойств(Пакет, Выборка);
		Если ЗначениеЗаполнено(Выборка.ПрикрепленныйФайл) Тогда
			Пакет.НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(Выборка.ПрикрепленныйФайл, "ДанныеФайла");
		Иначе
			Пакет.НавигационнаяСсылка = "";
		КонецЕсли;
		
		Результат.Добавить(Пакет, Выборка.Наименование);
		
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

// Заполняет параметр сеанса ПредставленияФайлов адресами картинок, представляющими аудиоданные и прочие файлы-не изображения
//
//Параметры:
//	нет
//
Процедура ОбновитьПредставленияФайлов() Экспорт
	
	// Все изображения будут храниться до завершения сеанса пользователя
	ИД = Новый УникальныйИдентификатор;
	
	ОбщиеИзображения = Новый Структура;
	ОбщиеИзображения.Вставить("Аудио",        ПоместитьВоВременноеХранилище(БиблиотекаКартинок.Аудиофайл.ПолучитьДвоичныеДанные(), ИД));
	ОбщиеИзображения.Вставить("Пусто",        ПоместитьВоВременноеХранилище(БиблиотекаКартинок.Пустая16.ПолучитьДвоичныеДанные(), ИД));
	ОбщиеИзображения.Вставить("Прочее",       ПоместитьВоВременноеХранилище(БиблиотекаКартинок.ДокументОбъект.ПолучитьДвоичныеДанные(), ИД));
	ОбщиеИзображения.Вставить("Добавить",     ПоместитьВоВременноеХранилище(БиблиотекаКартинок.ДобавитьЭлементВЯчейку.ПолучитьДвоичныеДанные(), ИД));
	
	ПараметрыСеанса.ПредставленияФайлов = Новый ФиксированнаяСтруктура(ОбщиеИзображения);
	
КонецПроцедуры

// Возвращает адрес или навигационную ссылку картинки, отображающей файл на форме
//	Для фотографий и видеозапсией возвращается их превью, для прочих файлов - общие картинки из
//	параметра сеанса ПредставленияФайлов
//
//Параметры:
//	СпособОткрытия - ПеречислениеСсылка.СпособОткрытияПрикрепленногоФайла - определяет тип файла
//	АдресДвоичныхДанных - навигационная ссылка или адрес в хранилище значений
//	ПлюсВместоПустогоФайла - Булево - нужно ли выводить "плюс", если АдресДвоичныхДанных ссылка пуст?
//
//Возвращаемое значение:
//	Строка - адрес или навигационная ссылка на картинку с представлением файла
Функция ПредставлениеФайлаНаФорме(СпособОткрытия, АдресДвоичныхДанных, ПлюсВместоПустогоФайла = Ложь) Экспорт

	Если ПлюсВместоПустогоФайла И Не ЗначениеЗаполнено(АдресДвоичныхДанных) Тогда
		Возврат ПараметрыСеанса.ПредставленияФайлов.Добавить;
	КонецЕсли;
	
	Если СпособОткрытия = Перечисления.СпособОткрытияПрикрепленногоФайла.КакИзображение
		Или СпособОткрытия = Перечисления.СпособОткрытияПрикрепленногоФайла.КакИзображение Тогда
		
		Если ЗначениеЗаполнено(АдресДвоичныхДанных) Тогда
			Возврат АдресДвоичныхДанных;
		Иначе
			Возврат ПараметрыСеанса.ПредставленияФайлов.Пусто;
		КонецЕсли;
		 
	ИначеЕсли СпособОткрытия = Перечисления.СпособОткрытияПрикрепленногоФайла.КакАудиоФайл Тогда
		
		Возврат ПараметрыСеанса.ПредставленияФайлов.Аудио;
		
	Иначе
		
		Возврат ПараметрыСеанса.ПредставленияФайлов.Прочее;
		
	КонецЕсли;

КонецФункции

// Возвращает картинку, отображающую содержимое медиа-файла
//
//Параметры:
//	СпособОткрытия - ПеречислениеСсылка.СпособОткрытияПрикрепленногоФайла - определяет тип файла
//	АдресДвоичныхДанных - навигационная ссылка или адрес в хранилище значений
//
//Возвращаемое значение:
//	Картинка 
Функция КартинкаМедиафайла(СпособОткрытия, АдресДвоичныхДанных) Экспорт

	Если СпособОткрытия = Перечисления.СпособОткрытияПрикрепленногоФайла.КакИзображение
		Или СпособОткрытия = Перечисления.СпособОткрытияПрикрепленногоФайла.КакИзображение Тогда
		
		Если ЭтоАдресВременногоХранилища(АдресДвоичныхДанных) Тогда
			Возврат Новый Картинка(ПолучитьИзВременногоХранилища(АдресДвоичныхДанных));
		Иначе
			Возврат БиблиотекаКартинок.Пустая16;
		КонецЕсли;
		 
	ИначеЕсли СпособОткрытия = Перечисления.СпособОткрытияПрикрепленногоФайла.КакАудиоФайл Тогда
		
		Возврат БиблиотекаКартинок.Аудиофайл;
		
	Иначе
		
		Возврат БиблиотекаКартинок.ДокументОбъект;
		
	КонецЕсли;

КонецФункции

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// Находит и удаляет записи справочника ПрикрепленныеФайлы, для которых в регистре сведений "ПринадлежностьФайлов"
//	не осталось ни одного владельца
//
//Параметры:
//	МассивФайлов - Массив, элементами которого являются ссылки справочника ПрикрепленныеФайлы
//
Процедура ПроверитьПринадлежностьФайловОбъектам(МассивФайлов) 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивФайлов", МассивФайлов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрикрепленныеФайлы.Ссылка КАК ЛишнийФайл
	|ИЗ
	|	Справочник.ПрикрепленныеФайлы КАК ПрикрепленныеФайлы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПринадлежностьФайлов КАК ПринадлежностьФайлов
	|		ПО ПринадлежностьФайлов.ПрикрепленныйФайл = ПрикрепленныеФайлы.Ссылка
	|ГДЕ
	|	ПрикрепленныеФайлы.Ссылка В (&МассивФайлов)
	|	И ПринадлежностьФайлов.ВладелецФайла ЕСТЬ NULL ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Выборка.ЛишнийФайл.ПолучитьОбъект().Удалить();
	КонецЦикла;
	
КонецПроцедуры
 

#КонецОбласти
