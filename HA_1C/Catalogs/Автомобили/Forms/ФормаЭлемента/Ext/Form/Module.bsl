
&НаКлиенте
// На мобильном приложении текущая строка теряется, поэтому сохраняем в переменной
Перем ТекСтрокаТЗ;


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Объект.Ссылка.Пустая() Тогда
		ЗаполнитьМежсервисныйИнтервал();
	КонецЕсли;
	УправлениеФормой();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРеквизитовКомандФормы

&НаКлиенте
Процедура АктивенПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ИзмерительТОПриИзменении(Элемент)
	УправлениеФормой();
	ЗаполнитьМежсервисныйИнтервал();
	
	Для каждого РегРабота Из Объект.РегламентныеРаботы Цикл
		РегРабота.Интервал = Объект.МежсервисныйИнтервал;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РегламентныеРаботыИнтервалОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Кратное12 = Текст % Объект.МежсервисныйИнтервал = 0;
	Если Не Кратное12 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Выбранное значение должно быть кратно межсервисному интервалу'; en = 'You must choose a value relevant to years span'"));
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = Новый СписокЗначений;
		ДанныеВыбора.Добавить(Объект.МежсервисныйИнтервал);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиТЧРегламентныеРаботы

&НаКлиенте
Процедура ДобавитьРаботу(Команда)
	ТекСтрокаХарактеристики = Неопределено;
	ОткрытьФормуВыбора();
КонецПроцедуры

&НаКлиенте
Процедура РегламентныеРаботыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекСтрокаТЗ = Неопределено Тогда
		ТекСтрока = Объект.РегламентныеРаботы.Добавить();
		Элементы.РегламентныеРаботы.ТекущаяСтрока = ТекСтрока.ПолучитьИдентификатор();
	Иначе
		ТекСтрока = ТекСтрокаТЗ;
	КонецЕсли;
	Элементы.РегламентныеРаботы.ТекущийЭлемент = Неопределено;
	ТекСтрока.РегламентнаяРабота = ВыбранноеЗначение;
	
	ТекСтрока.Интервал = Объект.МежсервисныйИнтервал;

КонецПроцедуры

&НаКлиенте
Процедура РегламентныеРаботыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	//Если Не ЗначениеЗаполнено(Элемент.ТекущиеДанные.РегламентнаяРабота) Тогда
	//	СтандартнаяОбработка = Ложь;
	//	ТекСтрокаТЗ = Элементы.РегламентныеРаботы.ТекущиеДанные;
	//	ОткрытьФормуВыбора();
	//КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФормы

Процедура УправлениеФормой()

	Элементы.ГруппаРегламентныеРаботы.Доступность = Объект.Активен;
	Элементы.ДатаПриобретения.Доступность = Объект.Активен;
	Элементы.Наименование.Доступность = Объект.Активен;
	
	Если Объект.ИзмерительТО = "ВРЕМЯ" Тогда
		Элементы.РегламентныеРаботыИнтервал.Заголовок = НСтр("ru = 'Интервал (месяцев)'; en = 'Span (months)'");
	ИначеЕсли Объект.ИзмерительТО = "ПРОБЕГ" Тогда
		Элементы.РегламентныеРаботыИнтервал.Заголовок = НСтр("ru = 'Пробег (км)'; en = 'Mileage (km)'");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбора()
	
	ОткрытьФорму("Справочник.РегламентныеРаботы.ФормаВыбора",, Элементы.РегламентныеРаботы);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМежсервисныйИнтервал()
	
	Если Объект.ИзмерительТО = "ВРЕМЯ" Тогда
		Объект.МежсервисныйИнтервал = 12;
	ИначеЕсли Объект.ИзмерительТО = "ПРОБЕГ" Тогда
		Объект.МежсервисныйИнтервал = 15000;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти